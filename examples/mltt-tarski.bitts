(* MLTT with a cumulative hierarchy of Tarski-style universes and universe polymorphism *)

(* Judgment forms *)
sort Ty ()
sort Tm (A : Ty)

sort Lvl ()
constructor 0ₗ () () : Lvl
constructor Sₗ () (_ : Lvl) : Lvl


(* Taski-style universe *)
constructor U () (l : Lvl) : Ty
destructor El (l : Lvl) [A : Tm(U(l))] () : Ty


(* we must declare the level l as an index, otherwise l is omitted from the
   syntax and we cannot write the rule  El(𝑢) --> U(?) because we don't know
   what to put in the place of ? *)
constructor 𝑢 () (l : Lvl) (Sₗ(l) / l : Lvl) : Tm(U(l))
equation El(𝑢(l)) --> U(l)

constructor ↑ (i : Lvl) (a : Tm(U(i))) : Tm(U(Sₗ(i)))
equation skipcheck El(↑(a)) --> El(a)


(* Dependent products *)
constructor Π   ()
                (A : Ty, B{x : Tm(A)} : Ty)
                : Ty

constructor λ   (A : Ty, B{x : Tm(A)} : Ty)
                (t{x : Tm(A)} : Tm(B{x}))
                : Tm(Π(A, x. B{x}))

destructor ﹫   (A : Ty, B{x : Tm(A)} : Ty)
                [t : Tm(Π(A, x. B{x}))]
                (u : Tm(A))
                : Tm(B{u})

equation ﹫(λ(x. t{x}), u) --> t{u}

(* code for Π in U.
   note that π requires the codes a and b to be in the same universe, which
   can always be achieved by lifting the smaller one *)
constructor π(i : Lvl) (a : Tm(U(i)), b{_ : Tm(El(a))} : Tm(U(i))) : Tm(U(i))

equation El(π(a, x.b{x})) --> Π(El(a), x. El(b{x}))

(* some authors consider a stronger form of cumulativity in which we have
   ↑(π(a, x.b{x})) = π(↑(a), x. ↑(b{x})). we unfortunately cannot have
   it here because ↑ and π are both constructors, and therefore cannot be the head
   of a rewrite rule. note that we could also declare ↑ as a destructor, allowing
   us to have the rule ↑(π(a, x.b{x})) --> π(↑(a),x.↑(b{x})). however, in
   this case the rule El(↑(a)) --> El(a) would not be valid anymore *)


(* Natural numbers *)
constructor ℕ () () : Ty
constructor 0 () () : Tm(ℕ)
constructor S () (n : Tm(ℕ)) : Tm(ℕ)

destructor ind_ℕ    ()
                    [n : Tm(ℕ)]
                    (P {x : Tm(ℕ)} : Ty,
                     p0 : Tm(P{0}),
                     ps {n : Tm(ℕ), pn : Tm(P{n})} : Tm(P{S(n)}))
                (* ------------ *)
                    : Tm(P{n})

equation ind_ℕ(0, x. P{x}, p0, n pn. ps{n, pn}) --> p0
equation ind_ℕ(S(n), x. P{x}, p0, n pn. ps{n, pn}) -->
    ps{n, ind_ℕ(n, x. P{x}, p0, n pn. ps{n, pn})}

(* code in U for ℕ *)
constructor nat () () (0ₗ / i : Lvl) : Tm(U(i))

equation El(nat) --> ℕ


(* universe polymorphism (quantification over levels) *)
constructor ∀ () (A {i : Lvl} : Ty) : Ty
constructor Λ (A {i : Lvl} : Ty) (t{i : Lvl} : Tm(A{i}))
        : Tm(∀(i. A{i}))
destructor inst (A {i : Lvl} : Ty) [t : Tm(∀(i. A{i}))] (l : Lvl)
        : Tm(A{l})
equation inst(Λ(i. t{i}), l) --> t{l}


(* universe polymorphic id function *)
(* the sort is convertible to Tm(∀(i. Π(U(i), a. Π(El(a), _. El(a))))) *)
let id_poly : Tm(∀(i. El(π(𝑢(i), a. ↑(π(a, _. a))) :: Tm(U(Sₗ(i))))))
  :=  Λ(i. λ(a. λ(x. x)))

(* applying it to 0 *)
let 0' : Tm(ℕ) := ﹫(﹫(inst(id_poly, 0ₗ), nat), 0)
assert 0' = 0
