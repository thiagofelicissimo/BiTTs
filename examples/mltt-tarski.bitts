(* MLTT with a cumulative hierarchy of Tarski-style universes and universe polymorphism *)

(* Judgment forms *)
sort Ty ()
sort Tm (A : Ty)

sort Lvl ()
constructor 0â‚— () () : Lvl
constructor Sâ‚— () (_ : Lvl) : Lvl


(* Taski-style universe *)
constructor U () (l : Lvl) : Ty
destructor El (l : Lvl) [A : Tm(U(l))] () : Ty


(* we must declare the level l as an index, otherwise l is omitted from the
   syntax and we cannot write the rule  El(ğ‘¢) --> U(?) because we don't know
   what to put in the place of ? *)
constructor ğ‘¢ () (l : Lvl) (Sâ‚—(l) / l : Lvl) : Tm(U(l))
equation El(ğ‘¢(l)) --> U(l)

constructor â†‘ (i : Lvl) (a : Tm(U(i))) : Tm(U(Sâ‚—(i)))
equation skipcheck El(â†‘(a)) --> El(a)


(* Dependent products *)
constructor Î    ()
                (A : Ty, B{x : Tm(A)} : Ty)
                : Ty

constructor Î»   (A : Ty, B{x : Tm(A)} : Ty)
                (t{x : Tm(A)} : Tm(B{x}))
                : Tm(Î (A, x. B{x}))

destructor ï¹«   (A : Ty, B{x : Tm(A)} : Ty)
                [t : Tm(Î (A, x. B{x}))]
                (u : Tm(A))
                : Tm(B{u})

equation ï¹«(Î»(x. t{x}), u) --> t{u}

(* code for Î  in U.
   note that Ï€ requires the codes a and b to be in the same universe, which
   can always be achieved by lifting the smaller one *)
constructor Ï€(i : Lvl) (a : Tm(U(i)), b{_ : Tm(El(a))} : Tm(U(i))) : Tm(U(i))

equation El(Ï€(a, x.b{x})) --> Î (El(a), x. El(b{x}))

(* some authors consider a stronger form of cumulativity in which we have
   â†‘(Ï€(a, x.b{x})) = Ï€(â†‘(a), x. â†‘(b{x})). we unfortunately cannot have
   it here because â†‘ and Ï€ are both constructors, and therefore cannot be the head
   of a rewrite rule. note that we could also declare â†‘ as a destructor, allowing
   us to have the rule â†‘(Ï€(a, x.b{x})) --> Ï€(â†‘(a),x.â†‘(b{x})). however, in
   this case the rule El(â†‘(a)) --> El(a) would not be valid anymore *)


(* Natural numbers *)
constructor â„• () () : Ty
constructor 0 () () : Tm(â„•)
constructor S () (n : Tm(â„•)) : Tm(â„•)

destructor ind_â„•    ()
                    [n : Tm(â„•)]
                    (P {x : Tm(â„•)} : Ty,
                     p0 : Tm(P{0}),
                     ps {n : Tm(â„•), pn : Tm(P{n})} : Tm(P{S(n)}))
                (* ------------ *)
                    : Tm(P{n})

equation ind_â„•(0, x. P{x}, p0, n pn. ps{n, pn}) --> p0
equation ind_â„•(S(n), x. P{x}, p0, n pn. ps{n, pn}) -->
    ps{n, ind_â„•(n, x. P{x}, p0, n pn. ps{n, pn})}

(* code in U for â„• *)
constructor nat () () (0â‚— / i : Lvl) : Tm(U(i))

equation El(nat) --> â„•


(* universe polymorphism (quantification over levels) *)
constructor âˆ€ () (A {i : Lvl} : Ty) : Ty
constructor Î› (A {i : Lvl} : Ty) (t{i : Lvl} : Tm(A{i}))
        : Tm(âˆ€(i. A{i}))
destructor inst (A {i : Lvl} : Ty) [t : Tm(âˆ€(i. A{i}))] (l : Lvl)
        : Tm(A{l})
equation inst(Î›(i. t{i}), l) --> t{l}


(* universe polymorphic id function *)
(* the sort is convertible to Tm(âˆ€(i. Î (U(i), a. Î (El(a), _. El(a))))) *)
let id_poly : Tm(âˆ€(i. El(Ï€(ğ‘¢(i), a. â†‘(Ï€(a, _. a))) :: Tm(U(Sâ‚—(i))))))
  :=  Î›(i. Î»(a. Î»(x. x)))

(* applying it to 0 *)
let 0' : Tm(â„•) := ï¹«(ï¹«(inst(id_poly, 0â‚—), nat), 0)
assert 0' = 0
